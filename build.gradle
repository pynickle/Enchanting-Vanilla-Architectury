plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            // JEI
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven {
            // JEI
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        maven {
            // YACL
            name 'Xander Maven'
            url 'https://maven.isxander.dev/releases'
        }
        exclusiveContent {
            // Curse Maven
            forRepository {
                maven {
                    url "https://cursemaven.com"
                }
            }
            filter {
                includeGroup "curse.maven"
            }
        }
        exclusiveContent {
            // Soul fire'd
            forRepository {
                maven {
                    name = "Crystal Nest"
                    url = "https://maven.crystalnest.it"
                }
            }
            filter {
                includeGroup "it.crystalnest"
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        // mappings loom.officialMojangMappings()

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.21.6:2025.06.29@zip")
        }
        // mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}"
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
        }
    }
}
